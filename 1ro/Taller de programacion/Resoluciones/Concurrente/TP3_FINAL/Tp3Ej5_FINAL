programa Tp3Ej5_FINAL
procesos
  proceso buscarUnPapel(E quienSoy:numero;ES termine: boolean;E aveOrigen:numero;E calleOrigen:numero)
  comenzar
    BloquearEsquina(11,11)
    Pos(11,11)
    si HayPapelEnLaEsquina
      tomarPapel  
    sino
      termine:=V
    Pos(aveOrigen,calleOrigen)
    LiberarEsquina(11,11)
  fin
areas
  avenida4: AreaP (4,1,4,100)
  avenida6: AreaP (6,1,6,100)
  avenida8: AreaP (8,1,8,100)
  avenida10: AreaP (10,1,10,100)
  puntoComun: AreaC (11,11,11,11)
  areaCoordinador: AreaP (1,1,1,1)
robots
  robot tipoCompetidor
  variables
    quienSoy,aveOrigen,calleOrigen: numero
    termine:boolean
  comenzar
    RecibirMensaje(quienSoy,coordinador)
    termine:=F
    aveOrigen:=PosAv
    mientras (PosCa<100)&(~termine)
      calleOrigen:=PosCa
      buscarUnPapel(quienSoy,termine,aveOrigen,calleOrigen)
      si (~termine)
        depositarPapel
        mover
    si ~termine {si calle=100}
      calleOrigen:=PosCa
      buscarUnPapel(quienSoy,termine,aveOrigen,calleOrigen)
      si (~termine)
        depositarPapel
    EnviarMensaje(quienSoy,coordinador)
    EnviarMensaje(calleOrigen,coordinador)
  fin
  robot tipoCoordinador
  variables
    quien,calle,distMax,ganador:numero
  comenzar
    EnviarMensaje(1,competidor1)
    EnviarMensaje(2,competidor2)
    EnviarMensaje(3,competidor3)
    EnviarMensaje(4,competidor4)
    distMax:=-1
    repetir 4
      RecibirMensaje(quien,*)
      si (quien=1)
        RecibirMensaje(calle,competidor1)
      sino
        si (quien=2)
          RecibirMensaje(calle,competidor2)
        sino
          si (quien=3)
            RecibirMensaje(calle,competidor3)
          sino
            RecibirMensaje(calle,competidor4)
      si (calle>distMax)
        distMax:=calle
        ganador:=quien
    Informar('Ganador_',ganador)
  fin
variables
  competidor1,competidor2,competidor3,competidor4: tipoCompetidor
  coordinador: tipoCoordinador
comenzar
  AsignarArea(competidor1, avenida4)
  AsignarArea(competidor2, avenida6)
  AsignarArea(competidor3, avenida8)
  AsignarArea(competidor4, avenida10)
  AsignarArea(coordinador,areaCoordinador )
  AsignarArea(coordinador,puntoComun )
  AsignarArea(competidor1,puntoComun )
  AsignarArea(competidor2,puntoComun )
  AsignarArea(competidor3,puntoComun )
  AsignarArea(competidor4,puntoComun )
  Iniciar(coordinador,1,1)
  Iniciar(competidor1,4,1)
  Iniciar(competidor2,6,1)
  Iniciar(competidor3,8,1)
  Iniciar(competidor4,10,1)
fin