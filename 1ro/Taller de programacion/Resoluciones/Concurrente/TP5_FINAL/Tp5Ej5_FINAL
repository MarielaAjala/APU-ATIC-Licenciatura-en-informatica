programa Tp5Ej5_FINAL
procesos
  proceso juntarPapeles {Â¡Lo realiza el servidor para tener la bolsa cargada al iniciar!}
  comenzar
    mientras HayPapelEnLaEsquina
      tomarPapel
  fin
  proceso enviarIds
  comenzar
    EnviarMensaje(1,cliente1)
    EnviarMensaje(2,cliente2)
    EnviarMensaje(3,cliente3)
  fin
  proceso juntaryDepositarPapelesDeDepot (E cantDepositada:numero;E aveOrigen:numero;E calleOrigen:numero)
  comenzar
    repetir cantDepositada
      BloquearEsquina(100,1)
      Pos(100,1)
      tomarPapel
      Pos(aveOrigen,calleOrigen)
      LiberarEsquina(100,1)
      depositarPapel
  fin
  proceso depositarPapelesEnDepot (E cantPedida:numero;E aveOrigen:numero;E calleOrigen:numero)
  comenzar
    BloquearEsquina(100,1)
    Pos(100,1)
    repetir cantPedida
      depositarPapel
    Pos(aveOrigen,calleOrigen)
    LiberarEsquina(100,1)
  fin
areas
  deposito: AreaC (100,1,100,1)
  areaCliente1: AreaP (10,1,10,1)
  areaCliente2: AreaP (11,1,11,1)
  areaCliente3: AreaP (12,1,12,1)
  areaServidor: AreaP (13,1,13,1)
robots
  robot tipoServidor
  variables
    cantDepositada,quien,aveOrigen,calleOrigen:numero
  comenzar
    aveOrigen:=PosAv
    calleOrigen:=PosCa
    juntarPapeles
    enviarIds
    repetir 12
      RecibirMensaje(quien,*)
      Random(cantDepositada,1,5)
      depositarPapelesEnDepot(cantDepositada,aveOrigen,calleOrigen)
      si (quien=1)       
        EnviarMensaje(cantDepositada,cliente1)
      sino
        si (quien=2)
          EnviarMensaje(cantDepositada,cliente2)
        sino
          EnviarMensaje(cantDepositada,cliente3)    
  fin
  robot tipoCliente
  variables
    quienSoy,ackNum,aveOrigen,calleOrigen: numero
  comenzar
    RecibirMensaje(quienSoy,servidor)
    aveOrigen:=PosAv
    calleOrigen:=PosCa
    repetir 4
      EnviarMensaje(quienSoy,servidor)
      RecibirMensaje(ackNum,servidor)
      juntaryDepositarPapelesDeDepot(ackNum,aveOrigen,calleOrigen)
  fin
variables
  cliente1,cliente2,cliente3: tipoCliente
  servidor: tipoServidor
comenzar
  AsignarArea(cliente1,areaCliente1 )
  AsignarArea(cliente1,deposito )
  AsignarArea(cliente2,areaCliente2 )
  AsignarArea(cliente2,deposito )
  AsignarArea(cliente3,areaCliente3 )
  AsignarArea(cliente3,deposito )
  AsignarArea(servidor,areaServidor )
  AsignarArea(servidor,deposito )
  Iniciar (servidor,13,1)
  Iniciar (cliente1,10,1)
  Iniciar (cliente2,11,1)
  Iniciar (cliente3,12,1)
fin