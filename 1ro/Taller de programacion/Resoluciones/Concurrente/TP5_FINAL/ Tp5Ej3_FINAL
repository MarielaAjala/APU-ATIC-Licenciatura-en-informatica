programa Tp5Ej3_FINAL
procesos
  proceso depositarFlores(E aveOrigen:numero; E calleOrigen:numero)
  comenzar
    BloquearEsquina (10,10)
    Pos (10,10)
    mientras (HayFlorEnLaBolsa)
      depositarFlor
    Pos (aveOrigen,calleOrigen)
    LiberarEsquina (10,10)
  fin
  proceso cosecharFlores(E cantidad:numero; ES total:numero)
  comenzar
    BloquearEsquina (10,10)
    Pos (10,10)
    repetir cantidad
      tomarFlor
    total:=total+cantidad
    Pos (1,5)
    LiberarEsquina (10,10)
  fin
  proceso comunicarAlCosechador(E quienSoy:numero; E finalice:boolean;E cantFlores:numero)
  comenzar
    EnviarMensaje (quienSoy,cosechador)  
    EnviarMensaje (finalice,cosechador)
    EnviarMensaje(cantFlores,cosechador)
  fin
  proceso llegarAlObjetivo(ES cantFlores:numero;ES cantAvances:numero;ES termine:boolean)
  comenzar
    mientras (cantAvances<5)&(cantFlores<10)&(~termine)
      si (HayFlorEnLaEsquina)
        tomarFlor 
        cantFlores:=cantFlores+1 
      sino
        si(PosAv<100)
          mover
          cantAvances:=cantAvances+1
        sino
          termine:=V
  fin
areas
  areaCosechador: AreaP (1,5,1,5)
  calleRecolector1: AreaPC (1,3,100,3)
  calleRecolector2: AreaPC (1,4,100,4)
  deposito: AreaC (10,10,10,10)
robots
  robot tipoRecolector
  variables
    quienSoy,cantFlores,cantAvances,aveOrigen,calleOrigen: numero
    finalice:boolean
  comenzar
    calleOrigen:=PosCa
    aveOrigen:=PosAv
    derecha
    RecibirMensaje (quienSoy,cosechador)
    cantFlores:=0
    cantAvances:=0
    finalice:=F
    llegarAlObjetivo(cantFlores,cantAvances,finalice)
    mientras (~finalice) 
      aveOrigen:=PosAv
      si (cantFlores>0)
        depositarFlores (aveOrigen,calleOrigen) {Junte 10 flores, a pesar de que hay mas}     
        comunicarAlCosechador(quienSoy,finalice,cantFlores)
        cantFlores:=0
      cantAvances:=0
      llegarAlObjetivo(cantFlores,cantAvances,finalice)
    aveOrigen:=PosAv
    si (cantFlores>0)
      depositarFlores (aveOrigen,calleOrigen)
    comunicarAlCosechador(quienSoy,finalice,cantFlores)
  fin
  robot tipoCosechador
  variables
    quien,terminaron,total,floresRecogidas :numero
    finalizo:boolean
  comenzar
    EnviarMensaje(1,recolector1)
    EnviarMensaje(2,recolector2)
    terminaron:=0
    total:=0
    mientras (terminaron<2)
      RecibirMensaje (quien,*)
      si (quien=1)
        RecibirMensaje(finalizo,recolector1)
        RecibirMensaje (floresRecogidas,recolector1)  
        si finalizo
          terminaron:=terminaron + 1
      sino
        RecibirMensaje(finalizo,recolector2)
        RecibirMensaje (floresRecogidas,recolector2)
        si finalizo
          terminaron:=terminaron + 1
      cosecharFlores(floresRecogidas,total)
    Informar ('Cantidad_cosechada_',total)     
  fin
variables
  recolector1,recolector2:tipoRecolector
  cosechador:tipoCosechador
comenzar
  AsignarArea (cosechador,areaCosechador)
  AsignarArea (recolector1,calleRecolector1)
  AsignarArea (recolector2,calleRecolector2)
  AsignarArea (cosechador,deposito)
  AsignarArea (recolector1,deposito)
  AsignarArea (recolector2,deposito)
  Iniciar (cosechador,1,5)
  Iniciar (recolector1,1,3)
  Iniciar (recolector2,1,4)
fin
